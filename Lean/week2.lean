/- # **Functions and logical implications**

**Author:** Florent Schaffhauser, Uni-Heidelberg, Summer Semester 2023

Our goal in this file is to keep exploring basic definitions and tactics in Lean. It is a direct follow-up to the previous [*Introduction to Lean*](https://github.com/matematiflo/Comp_assisted_math/blob/2023_SoSe/Lean/week1.md).

The main objective today is to learn about functions:

* How to define basic functions in Lean
* What it means to *curry* and *uncurry* a function of two variables
* How to use functions to write and prove basic logical implications

As a matter of fact, we have already seen an important example of a function, in the [*modus ponens* file](https://github.com/matematiflo/Comp_assisted_math/blob/2023_SoSe/Lean/Practice_folder/modus_ponens.lean), where we also learned about the following tactics:

* `exact`
* `apply`
* `intro`
* `cases`

In what follows, we will encounter new tactics:

* `show`
* `split`
* `by_contradiction`

Recall that in [week1.lean](https://github.com/matematiflo/Comp_assisted_math/blob/2023_SoSe/Lean/week1.lean), we also learned about the `reflexivity` tactic, usually abbreaviated to `refl`.

We start by importing the `data.nat.pow` library, which will be used in this file. The `import` command must be used at the beginning of the .lean file. 

As a consequence of this import, the basic `goals accomplished` message of Lean gets upgraded to the fancy `goals accomplished ðŸŽ‰` version :-) -/

import data.nat.pow
import data.real.basic

/- ## **Defining a function**

There are several ways to define a function in Lean, all of them useful depending on the context.

The basic type-theoretical aspect behind what we are about to see is that, if `X` and `Y` are types, then we can construct terms of type `X â†’ Y`, which is the type of functions from `X` to `Y` (the symbol `â†’` is called a *constructor*).

We have already seen several types, for instance `â„•` (the type of natural numbers), `â„¤` (the type of integers), but also `Prop`, `char`, `string` or `list â„•`. A `#check` command on any one of those will reveal that these types are all terms of type `Type` (note the capitalized `T` in `Type` here), which is the type with which we will be working most of the time. Indeed, if `X` and `Y` are terms of type `Type`, then the type `X â†’ Y` is also a term of type `Type`, as we shall see in the examples below. -/

/- ### First definition of a function

The basic syntax presented below should be thought of as a precise version of the intuitive notation `fâ‚(n) := - 2 * n + 5`, which means that `fâ‚(n)` is *defined* to be equal `- 2 * n + 5`. This is certainly meaningful if we specify that `n` is a term for which the expression `- 2 * n + 5` makes sense, for instance `n : â„¤`, in which case `- 2 * n + 5` is also a term of type `â„¤`. So this will define a term `fâ‚ : â„¤ â†’ â„¤`, Â¡.e. a function from `â„¤` to `â„¤`.

The notation fâ‚ obtained via `f\1`.

Of course we could also want to define `fâ‚` as a function `â„• â†’ â„¤` (i.e. a sequence of integers), in which case we need to be more careful with the syntax. Here is a definition of such a function `fâ‚` that is fully precise, in which `n` is a term of type `â„•` and `fâ‚(n)` (also denoted simply by `fâ‚ n`, without a need for parentheses) is a term of type `â„¤`. -/

def fâ‚ (n : â„•) : â„¤ := - 2 * n + 5

#check fâ‚
#check fâ‚ 5
#eval fâ‚ 5

/- Note that, if you print `fâ‚`, you will see that Lean introduces a symbol `â†‘`, which is a *coercion*. meaning that the natural number `n : â„•` is *coerced* (forcely converted) into an integer `n : â„¤`, for which the term `- 2 * n + 5` makes sense. -/

-- #print fâ‚

/- If we declare `fâ‚` to be a function from `â„¤` to `â„¤` instead, then we do not need to indicate the type of `fâ‚ n` in the definition, because for `n` of type `â„¤`, the term `- 2 * n + 5` is automatically recognised as being of type `â„¤`. -/

def f (n : â„¤) := - 2 * n + 5

#check f
#check f 5
#eval f 5

/- ### Second definition of a function

Next, we define the same function one more time, but in a slightly different way, emphasising in the declaration that `fâ‚‚` is a term of type `â„• â†’ â„¤`. This makes the first part of the defintion perhaps slightly more readable but then, after the `:=` sign, we have to introduce the term `n` using the `Î»` notation, which gives its name to [`Î»`-calculus](https://en.wikipedia.org/wiki/Lambda_calculus#Origin_of_the_Î»_symbol).

In the definition of `fâ‚‚`, I indicated the coercion symbol `â†‘` explicitly, but it is not necessary (the symbol `â†‘` is obtained by typing in `\u`, as in *up*). -/

def fâ‚‚ : â„• â†’ â„¤ := Î» n, -2 * â†‘n + 5

#check fâ‚‚
#check fâ‚‚ 5
#eval fâ‚‚ 5

/- Using the command `#print` reveals that `fâ‚` and `fâ‚‚` are indeed the same function, and this is something we can actually prove. Since this is true *by definition*, we can prove it using the `refl` tactic. -/

-- #print fâ‚‚

def f1_and_f2_are_the_same_function : fâ‚ = fâ‚‚ := by { refl }

/- Note that `refl` can also be used to prove basic results such as the fact that the value of `fâ‚ 5` is equal to `-5`. Since we have no need of a name for this result, we simply declare it as an `example`, which is similar to `def` but without having to give a name to what we are defining (this implies that we cannot perform a `#check` or a `#print` afterwards). -/

example : fâ‚ 5 = -5 := by { refl }

/- ### Third definition of a function

In this third option, we do not specify the type of `fâ‚ƒ` before the definition, so we have to be more detailed about the type of both `n` and `- 2 * n + 5` after the `:=` symbol. It is still the exact same function as `fâ‚` and `fâ‚‚`, though. -/

def fâ‚ƒ := Î» (n : â„•), (-2 * n + 5 : â„¤)

example : fâ‚‚ = fâ‚ƒ := by { refl }

/- ### Exercise 1

> Given a type `X : Type`, define an "identity function" from `X` to itself.
-/

/- ### Currying and uncurrying

We are used to defining functions of two or more variables as functions from a product set to another set. For instance, a real-valued function of two real variables can be formalised as term of type `â„Â² â†’ â„` (the superscript notation `Â²` is obtained by typing in `\^2` followed by a space character).

To define a term of type `â„Â² â†’ â„`, we need to define the type `â„Â²` first. This is already programmed in `Lean`, via the `prod` construction: if `Tâ‚` and `Tâ‚‚` are types, there is a type `Tâ‚ Ã— Tâ‚‚` (or `prod Tâ‚ Tâ‚‚`; the notation `Ã—` is obtained by typing in `\x` followed by a space character), whose terms are pairs `(tâ‚, tâ‚‚)` such that `tâ‚` is a term of type `Tâ‚` and `tâ‚‚` is a term of type `Tâ‚‚`. -/

#check (3,4)

#check â„• Ã— â„¤  
#check ( (3, -4) : â„• Ã— â„¤ )

/- By construction, the type `Tâ‚ Ã— Tâ‚‚` comes equipped with two projection functions `fst : Tâ‚ Ã— Tâ‚‚ â†’ Tâ‚` and `snd : Tâ‚ Ã— Tâ‚‚ â†’ Tâ‚‚`, that we can use to define functions `h : Tâ‚ Ã— Tâ‚‚ â†’ Tâ‚ƒ`. -/

#check (3,4).fst
#check (3,4).snd

/- They can also be denoted by `.1` and `.2`. -/

#eval (3,4).1
#eval (3,4).2

def h : â„• Ã— â„• â†’ â„• := Î» x, 2 * x.1 + x.2

/- However, it is *much more convenient* to define a function of two variables `Tâ‚ Ã— Tâ‚‚ â†’ Tâ‚ƒ` as a function `Tâ‚ â†’  (Tâ‚‚ â†’ Tâ‚ƒ)`, meaning a function from `Tâ‚` to the functions `Tâ‚‚ â†’ Tâ‚ƒ`. -/

def g : â„• â†’ (â„• â†’ â„•) := Î» n m, 2 * n + m

#check g
#eval g 1 3

#check h
#eval h (1, 3)

/- The precise correspondence between terms `h` of type `Tâ‚ Ã— Tâ‚‚ â†’ Tâ‚ƒ` and terms `g` of type `Tâ‚ â†’ (Tâ‚‚ Ã— Tâ‚ƒ)` is given as follows.

If `h : Tâ‚ Ã— Tâ‚‚ â†’ Tâ‚ƒ`, we define, for `tâ‚ : Tâ‚` the term `(g tâ‚) : Tâ‚‚ â†’ Tâ‚ƒ` as the function sending `tâ‚‚ : Tâ‚‚` to the term `(g tâ‚) tâ‚‚ := h (tâ‚, tâ‚‚)`, which is indeed of type `Tâ‚ƒ`.

Conversely, to a term `g : Tâ‚ â†’ (Tâ‚‚ â†’ Tâ‚ƒ)`, we can associate a term `h` of type `Tâ‚ Ã— Tâ‚‚ â†’ Tâ‚ƒ` by setting `h (tâ‚, tâ‚‚) := (g tâ‚) tâ‚‚`. Since `(g tâ‚)` is a function from `Tâ‚‚` to `Tâ‚ƒ`, the term `h (tâ‚, tâ‚‚)` thus defined is indeed of type `Tâ‚ƒ`.

The process of replacing `h : Tâ‚ Ã— Tâ‚‚ â†’ Tâ‚ƒ` by `g : Tâ‚ â†’ (Tâ‚‚ â†’ Tâ‚ƒ)` is known as [*currying*](https://en.wikipedia.org/wiki/Currying). The reverse process is known as *uncurrying*.

This is basically saying that we can replace a function of two variables `h` from `Tâ‚ Ã— Tâ‚‚` to `Tâ‚ƒ` by a *family* `g` (indexed by `Tâ‚`) of [*partial applications*](https://en.wikipedia.org/wiki/Partial_application) from `Tâ‚‚` to `Tâ‚ƒ`.

Here is for instance a family of sequences of natural numbers. This uses the power function (denoted using the caret symbol `^`), which was imported at the beginning of this file via the `import data.nat.pow` command. -/

def u (q : â„•) (n : â„•) : â„• := q^n

variable (q : â„•) 

#check u q
#eval u 2 4

/- It is easy to iterate this process and define functions  of three variables or more. The upshot is that, under this form, functions of three real variables can be defined without defining a type `â„Â³` first. -/

def F ( x y z : â„ ) : â„ := x^2 - y + z

#check F

/- ### Exercise 2

> Given types `X Y Z : Types`, define a function `compose` sending functions `f : X â†’ Y` and `g : Y â†’ Z` to a function `compose f g` from `X` to `Z`, sending a term `x : X` to the term `g (f x)`.  

### ***Logical implications***

### Negatives

There is a proposition (i.e. a term of type `Prop`) called `false` and characterised by the fact that it does not admit a proof. In other words, `false` is a type that has no terms.

Technically, the type `false` is defined as an *inductive type* (we will come back to that later). For now, we will use the type `false` to define negatives.

Indeed, in Lean, given a term `P : Prop`, the type `Â¬P` is *defined* as the type `P â†’ false` (i.e. the type of functions from `P` to `false`). In other words, supplying a proof of `Â¬P` means supplying a proof that a to a proof of `P` there is associated a proof of `false`.

It will take a bit of time to wrap our heads around that, so let us start manipulating this notion. The symbol `Â¬` is obtained by typing in `\n` or `\not`, followed by  a space character. -/

#check false

variables P : Prop
#check Â¬P

/- Here is an example of a statement using negatives. It is known as *modus tollens* and it says that `( (P â†’ Q) âˆ§ Â¬Q ) â†’ Â¬P`. In other words, if we can prove `P â†’ Q` and `Â¬Q`, then we can prove `Â¬Q`.

In order to write a proof of this statement (and many other involving `Â¬`), it is useful to keep in mind that a term of type `Â¬P` is (by definition) a term of type `P â†’ false`, meaning a *function* from `P` to `false`, and that it should be manipulated as such. -/

def MT { P Q : Prop } ( hPQ : P â†’ Q ) ( nq : Â¬Q ) : Â¬P :=
begin
  -- show P â†’ false,
  intro p,
  apply nq,
  apply hPQ,
  exact p,
end

/- The `show` tactic that was used in the proof above is facultative: it enables us to visualise better what we are supposed to prove. It works if, when you write `show t`, Lean is able to find a term that matches `t` in the target.

### The contrapositive

As an application of our definition of the function `MT`, we can prove the implication `(P â†’ Q) â†’ (Â¬Q â†’ Â¬P)`. -/

#check @MT

def contrapositive  { P Q : Prop } : ( P â†’ Q ) â†’ ( Â¬Q â†’ Â¬P ) :=
begin
  intro hPQ,
  intro nq,
  exact MT hPQ nq,
end

/- The implication `Â¬Q â†’ Â¬P` is known as the [*contrapositive*](https://en.wikipedia.org/wiki/Contraposition#Formal_definition) of the implication `P â†’ Q`.

In practice, it is often used to prove `P â†’ Q` because, if we apply contraposition to `Â¬Q â†’ Â¬P`, we find `Â¬Â¬P â†’ Â¬Â¬Q` and, in everyday mathematics, `Â¬Â¬P` is equivalent to `P`.

Again, this is a lot to digest, so let us break it down into several steps. -/

def contrapositive_with_double_neg { P Q : Prop } ( H : Â¬Q â†’  Â¬P ) : Â¬Â¬P â†’ Â¬Â¬Q :=
begin
  intro f,
  -- show Â¬Q â†’ false,
  intro x,
  apply f,
  apply H,
  exact x,
end

/- It is not necessary at this stage, but here is a shorter proof of the previous proposition, which says that `( Â¬Q â†’ Â¬P ) â†’ ( Â¬Â¬P â†’ Â¬Â¬Q )`. -/

def contrapositive_with_double_neg_again { P Q : Prop } ( H : Â¬Q â†’  Â¬P ) : Â¬Â¬P â†’ Â¬Â¬Q :=
begin
  intros f x,
  exact f (H x),
end

/- We now would like to be able to say that `Â¬Â¬P â†” P` (the left-right arrow `â†”` means *equivalence* and it is obtained by typing in `\lr`, followed by a space character).

More precisely, we want to prove `P â†’ Â¬Â¬P` and `Â¬Â¬P â†’ P`. Without getting too much into the details, let us just mention that there is a way to *split* the proof of an equivalence `P â†” Q` into two implications `P â†’ Q` and `Q â†’ P` and that this is what the `split` tactic does (see below).

Once we have two implications to prove, we can observe that the implication `P â†’ Â¬Â¬P` does not use any tactic: we prove it by showing that, to a proof of `P`, we can associate a proof of `Â¬P â†’ false`, which is the definition of `Â¬Â¬P`.

For the implication `Â¬Â¬P â†’ P`, however, more is needed. We have to argue *by contradiction*. This means that, after introducing a term `g` of type `Â¬Â¬P`, we replace the target `P` by `false` *while also adding a term* `h : Â¬P` (i.e. a proof of `P â†’ false`) to our local context. -/

def double_neg {P : Prop} : P â†” Â¬Â¬P :=
begin
  split,
  intro p,
  show Â¬P â†’ false,
  intro f,
  exact f p,
  intro g,
  by_contradiction,
  exact g h,
end

/- So, after using the tactic `by_contradiction` (which can be abbreviated to `by_contra`), we have proofs of *both* `Â¬Â¬P` and `Â¬P` in our local context, and this is how we produce a proof of `false`.

Since `false` is a type that has no terms, what we did means that adding a proof of `Â¬P` to our local context was, in a way, illicit. In other words, we cannot assume that `Â¬P` has a proof (for otherwise we get a proof of `false`) so this must mean that `P` has a proof.

This is indeed the case for us: we *are working* under the assumption that either `P` or `Â¬P` has a proof. This known as the [*Law of Excluded Middle*](https://en.wikipedia.org/wiki/Law_of_excluded_middle) (LEM) and [it is needed](https://en.wikipedia.org/wiki/Contraposition#Intuitionistic_logic) in order to prove the equivalence `(P â†’ Q) â†” (Â¬Q â†’ Â¬P)`.

Note that, since we are now equipped with the tactic `by_contradiction`, we can get rid of the double negatives in the proof of the implication `( Â¬Q â†’ Â¬P ) â†’ ( Â¬Â¬P â†’ Â¬Â¬Q )` and prove that `( Â¬Q â†’ Â¬P ) â†’ ( P â†’ Q )`. -/

def contra { P Q : Prop } ( H : Â¬Q â†’  Â¬P ) : P â†’ Q :=
begin
  intro p,
  by_contradiction,
  exact (H h) p,
end

/- ### Exercise 3

> Formalise the equivalence `( P â†’ Q ) â†” ( Â¬Q â†’ Â¬P )` and use the `split` tactic and the functions `MT` and `contra` defined above to write a proof of it.

---
-/